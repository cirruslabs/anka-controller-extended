apply plugin: "java-library"
apply plugin: "com.google.protobuf"
apply plugin: "com.jfrog.bintray"
apply plugin: "maven-publish"

ext {
  current_version = System.getenv().getOrDefault("CIRRUS_TAG", "SNAPSHOT")
}

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath libraries['com.jfrog.bintray.gradle:gradle-bintray-plugin']
  }
}


bintray {
  user = "fkorotkov"
  key  = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
  publications = ['maven']
  pkg {
    repo = 'maven'
    name = 'anka-controller-extended-api'
    userOrg = 'cirruslabs'
    licenses = ['MIT']
    vcsUrl = 'git@github.com:cirruslabs/anka-controller-extended.git'
    version {
      name = current_version
      released  = new Date()
    }
  }
}

task sourceJar(type: Jar) {
  from sourceSets.main.allSource
  classifier "sources"
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId 'org.cirruslabs.anka'
      artifactId 'controller-extended-api'
      version current_version
      from components.java
      artifact sourceJar
    }
  }
}

buildscript {
  dependencies {
    classpath libraries['com.google.protobuf:protobuf-gradle-plugin']
  }
}

protobuf {
  protoc {
    artifact = libraries['com.google.protobuf:protoc']
  }
  plugins {
    grpc {
      artifact = libraries['io.grpc:protoc-gen-grpc-java']
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}

sourceSets {
  main {
    java {
      srcDir file("${protobuf.generatedFilesBaseDir}/main/grpc")
      srcDir file("${protobuf.generatedFilesBaseDir}/main/java")
    }
  }
}

dependencies {
  api libraries['com.google.protobuf:protobuf-java']
  api libraries['io.grpc:grpc-auth']
  api libraries['io.grpc:grpc-core']
  api libraries['io.grpc:grpc-context']
  api libraries['io.grpc:grpc-netty']
  api libraries['io.grpc:grpc-protobuf']
  api libraries['io.grpc:grpc-stub']
}
