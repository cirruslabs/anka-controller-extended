apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: "com.jfrog.bintray"
apply plugin: "maven-publish"

ext {
  current_version = System.getenv().getOrDefault("CIRRUS_TAG", "SNAPSHOT")
}

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath libraries['com.jfrog.bintray.gradle:gradle-bintray-plugin']
  }
}


bintray {
  user = "fkorotkov"
  key  = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
  publications = ['maven']
  pkg {
    repo = 'maven'
    name = 'anka-sdk'
    userOrg = 'cirruslabs'
    licenses = ['MIT']
    vcsUrl = 'git@github.com:cirruslabs/anka-controller-extended.git'
    version {
      name = current_version
      released  = new Date()
    }
  }
}

task sourceJar(type: Jar) {
  from sourceSets.main.allSource
  classifier "sources"
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId 'org.cirruslabs.anka'
      artifactId 'sdk'
      version current_version
      from components.java
      artifact sourceJar
    }
  }
}

dependencies {
  implementation libraries['com.jcabi:jcabi-ssh']
  implementation libraries['org.jetbrains.kotlin:kotlin-stdlib']
  implementation libraries['org.jetbrains.kotlinx:kotlinx-coroutines-core']
  implementation libraries['org.jetbrains.kotlinx:kotlinx-coroutines-core-common']
  implementation libraries['org.jetbrains.kotlinx:kotlinx-coroutines-jdk8']
  implementation libraries['org.apache.httpcomponents:httpclient']
  implementation libraries['org.json:json']
}

kotlin {
  experimental {
    coroutines "enable"
  }
}
