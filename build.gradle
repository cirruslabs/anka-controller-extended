buildscript {
  repositories {
    mavenLocal()
    jcenter()
  }
  dependencies {
    classpath 'com.fkorotkov:gradle-libraries-plugin:0.1'
  }
}

apply plugin: 'com.fkorotkov.libraries'

updateLibraries.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['dev', 'eap', 'M1'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('dev version')
      }
    }
  }
}

allprojects {
  apply plugin: 'java-library'
  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  test {
    testLogging {
      exceptionFormat = 'full'
    }
  }
}

subprojects {
  buildscript {
    repositories {
      maven { setUrl("https://repo.gradle.org/gradle/repo") }
      maven { setUrl("https://plugins.gradle.org/m2/") }
    }

    // put on the classpath but do not apply it
    dependencies {
      classpath libraries['org.jetbrains.kotlin:kotlin-gradle-plugin']
    }
  }
  repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { setUrl("https://repo.gradle.org/gradle/repo") }
  }

  apply plugin: 'com.fkorotkov.libraries'
}
